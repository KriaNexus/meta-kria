#@TYPE: Machine
#@NAME: Generic kr260 Starter Kit
#@DESCRIPTION: Kria K26 SOM + kr260 carrier board

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-smk-kr:']['k26-smk-kr' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kr:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc

HDF_MACHINE = "k26-kr"

UBOOT_DT_FILES = "\
    zynqmp-sck-kr-g-revA.dts \
    zynqmp-sck-kr-g-revB.dts \
    "

IMAGE_BOOT_FILES += " devicetree/SMK-zynqmp-sck-kr-g-revB.dtb;system.dtb"

QB_OPT_APPEND += "\
    -device usb-hub,bus=usb3@0xFE200000.0,port=1,id=usb-hub \
    -device usb-storage,bus=usb3@0xFE200000.0,port=1.1,id=usbstick,drive=stick \
    -blockdev filename=${DEPLOY_DIR_IMAGE}/kr-eeprom.bin,node-name=i2c1.eeprom-51,driver=file \
"

QB_ROOTFS_OPT = "-drive file=@ROOTFS@,if=none,format=raw,id=stick"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_smk_kr']['k26-smk-kr' != "${MACHINE}"]}"
