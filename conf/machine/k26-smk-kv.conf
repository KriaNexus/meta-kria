#@TYPE: Machine
#@NAME: Generic kv260 Starter Kit
#@DESCRIPTION: Kria K26 SOM + kv260 carrier board

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-smk-kv:']['k26-smk-kv' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kv:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc
require conf/machine/include/kria-qemu-kv.inc

HDF_MACHINE = "k26-kv"

UBOOT_DT_FILES = "\
    zynqmp-sck-kv-g-revA.dts \
    zynqmp-sck-kv-g-revB.dts \
    "

# DTB_FILE_NAME cant be changed globally or it will break the build but we can modify it for these
# tasks specifically so that the resulting symlink points to the correct file for a specific board
DTB_FILE_NAME:task-install:pn-device-tree = "SMK-zynqmp-sck-kv-g-revB.dtb"
DTB_FILE_NAME:task-deploy:pn-device-tree = "SMK-zynqmp-sck-kv-g-revB.dtb"
# Workaround for bitbake not detecting the above change in the hash
PR:append:pn-device-tree = ".1"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_smk_kv']['k26-smk-kv' != "${MACHINE}"]}"
